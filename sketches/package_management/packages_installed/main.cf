body file control
{
      namespace => "cfdc_packages";
}

bundle agent installed(runenv, metadata, pkgs_add)
{
  classes:
      "$(vars)" expression => "default:runenv_$(runenv)_$(vars)";
      "not_$(vars)" expression => "!default:runenv_$(runenv)_$(vars)";

  vars:
      "vars" slist => { "@(default:$(runenv).env_vars)" };
      "$(vars)" string => "$(default:$(runenv).$(vars))";

      "activation_id" string => canonify("$(this.bundle)_$($(metadata)[activation][identifier])_$($(metadata)[activation][timestamp])");

  methods:
    verbose::
      "metadata" usebundle => default:report_metadata($(this.bundle), $(metadata)),
      inherit => "true";

  packages:
    not_test.redhat::
      "$(pkgs_add)"
      package_policy => "add",
      package_method => default:generic,
      handle => "$(activation_id)_packages_add_redhat";

      "$(pkgs_add)"
      package_policy => "verify",
      package_method => yum_rpm_verify,
      handle => "$(activation_id)_packages_verify_installed_redhat";

   not_test.!redhat::
      "$(pkgs_add)"
      package_policy => "add",
      package_method => default:generic,
      handle => "$(activation_id)_packages_add";

      "$(pkgs_add)"
      package_policy => "verify",
      package_method => default:generic,
      handle => "$(activation_id)_packages_verify_installed";

  reports:
    test::
      "$(this.bundle): In test mode, so simulating adding package $(pkgs_add)";
}

body package_method yum_rpm_verify
# TODO: implement package_noverify_returncode in stdlib and get rid of this special Red Hat verification case
{
  package_changes => "bulk";
  package_list_command => "/bin/rpm -qa --qf '%{name}.%{arch} %{version}-%{release}\n'";
  package_patch_list_command => "/usr/bin/yum --quiet check-update";

  package_list_name_regex    => "([^.]+).*";
  package_list_version_regex => "[^\s]\s+([^\s]+).*";
  package_list_arch_regex    => "[^.]+\.([^\s]+).*";

  package_installed_regex => ".*";
  package_name_convention => "$(name)-$(version).$(arch)";

  # set it to "0" to avoid caching of list during upgrade
  package_list_update_command => "/usr/bin/yum --quiet check-update";
  package_list_update_ifelapsed => "240";

  package_patch_installed_regex => "^\s.*";
  package_patch_name_regex    => "([^.]+).*";
  package_patch_version_regex => "[^\s]\s+([^\s]+).*";
  package_patch_arch_regex    => "[^.]+\.([^\s]+).*";

  package_add_command    => "/usr/bin/yum -y install";
  package_update_command => "/usr/bin/yum -y update";
  package_patch_command  => "/usr/bin/yum -y update";
  package_delete_command => "/bin/rpm -e --nodeps";
  package_verify_command => "/bin/rpm -V";
  package_noverify_returncode => "1";
}
